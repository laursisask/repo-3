buildscript {
  repositories {
    mavenCentral()
    jcenter()
  }

  dependencies {
    classpath "org.jfrog.buildinfo:build-info-extractor-gradle:4.7.3"
  }
}

allprojects {
    gradle.projectsEvaluated {
        tasks.withType(JavaCompile) {
            options.compilerArgs << "-Xlint:all,-processing"
        }
        tasks.withType(GroovyCompile) {
            options.compilerArgs << "-Xlint:all,-processing"
        }
    }

    tasks.matching { it instanceof Test }.all {
        testLogging.events = ["failed", "passed", "skipped", "standardOut", "standardError"]
        testLogging.showStandardStreams = true
    }

    configurations.all {
        transitive = true
    }
}

subprojects {
  group "com.trusona"
  version "3.2.1-SNAPSHOT"

  apply plugin: "java"
  apply plugin: "groovy"
  apply plugin: "signing"
  apply plugin: "maven"
  apply plugin: "maven-publish"
  apply plugin: "com.jfrog.artifactory"
  apply from: "../build-extras.gradle"

  sourceCompatibility = 1.7
  targetCompatibility = 1.7

  repositories {
    mavenCentral()
  }

  task "printVersion" {
    doLast {
      println project.version
    }
  }
}
