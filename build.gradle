buildscript {
  repositories {
    mavenCentral()
    jcenter()
  }

  dependencies {
    classpath "org.jfrog.buildinfo:build-info-extractor-gradle:4.7.3"
  }
}

allprojects {
  gradle.projectsEvaluated {
    tasks.withType(JavaCompile) {
      options.compilerArgs << "-Xlint:all,-processing"
    }
    tasks.withType(GroovyCompile) {
      options.compilerArgs << "-Xlint:all,-processing"
    }
  }

  tasks.matching { it instanceof Test }.all {
    testLogging.events = ["failed", "passed", "skipped", "standardOut", "standardError"]
    testLogging.showStandardStreams = true
  }
}

subprojects {
  group "com.trusona"
  version "3.3.0-SNAPSHOT"

  apply plugin: "java"
  apply plugin: "groovy"
  apply plugin: "signing"
  apply plugin: "maven"
  apply plugin: "maven-publish"
  apply plugin: "com.jfrog.artifactory"
  apply from: "../build-extras.gradle"

  sourceCompatibility = 1.7
  targetCompatibility = 1.7

  repositories {
    mavenCentral()
    jcenter()
  }

  task "printVersion" {
    doLast {
      println project.version
    }
  }

  uploadArchives {
    repositories {
      mavenDeployer {
        beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

        repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
          authentication(userName: ossrhUsername, password: ossrhPassword)
        }

        snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
          authentication(userName: ossrhUsername, password: ossrhPassword)
        }

        pom.project {
          name 'Trusona Server SDK for Java'
          artifactId 'trusona-sdk'
          packaging 'jar'

          description 'The Trusona Server SDK allows simplified interaction with the Trusona API.'
          url 'https://github.com/trusona/trusona-server-sdk-java'

          scm {
            developerConnection 'https://github.com/trusona/trusona-server-sdk-java.git'
            url 'https://github.com/trusona/trusona-server-sdk-java'
          }

          licenses {
            license {
              name 'The Apache License, Version 2.0'
              url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
            }
          }
        }
      }
    }
  }
}
